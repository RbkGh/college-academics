/*
 * Copyright (c) 2013-2016 Shaleen Jain <shaleen.jain95@gmail.com>
 *
 * This file is part of UPES Academics.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId 'com.shalzz.attendance'
        minSdkVersion 14
        targetSdkVersion 23

        resConfigs "en"
    }

    compileOptions {
        incremental true
    }

    dexOptions {
        dexInProcess true
        maxProcessCount = 4
    }

    productFlavors {
        dev {
            minSdkVersion 23
        }
        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion 14
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            resValue "string", "app_name", "UPES Academics (debug)"
            resValue "string", "contentAuthority", defaultConfig.applicationId + '.debug.provider'
            resValue "string", "account_type", "com.shalzz.debug"
            buildConfigField "String", "ACCOUNT_TYPE", '"com.shalzz.debug"'
        }
        release {
            minifyEnabled true
            useProguard true
            resValue "string", "app_name", "UPES Academics"
            resValue "string", "contentAuthority", defaultConfig.applicationId + '.provider'
            resValue "string", "account_type", "com.shalzz"
            buildConfigField "String", "ACCOUNT_TYPE", '"com.shalzz"'
            FileCollection proGuardFileCollection = files { file('./proguard').listFiles() }
            proguardFiles(proGuardFileCollection)
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    dependencies {
        final SUPPORT_LIBRARY_VERSION = '23.2.1'

        useLibrary 'org.apache.http.legacy'

        // google support libraries
        compile 'com.google.android.gms:play-services-analytics:8.+'
        compile 'com.google.android.gms:play-services-ads:8.+'
        compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
        compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
        compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
        compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
        compile "com.android.support:preference-v14:$SUPPORT_LIBRARY_VERSION"

        // 3rd party library projects
        compile 'com.jakewharton:butterknife:8.0.1'
        apt 'com.jakewharton:butterknife-compiler:8.0.1'
        compile 'com.android.volley:volley:1.0.0'
        compile 'com.squareup.okhttp:okhttp:2.5.0'
        compile 'com.github.amlcurran.showcaseview:library:5.4.0'
        compile 'com.google.code.gson:gson:2.6.1'
        compile('com.github.afollestad.material-dialogs:core:0.8.5.5@aar') {
            transitive = true
        }
        compile('de.psdev.licensesdialog:licensesdialog:1.8.0') {
            exclude module: 'support-v4'
        }
    }
}
